buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url "https://jitpack.io"
      content {
        excludeGroup "com.github.ben-manes.versions"
      }
    }

  }
  dependencies {
    classpath group: 'cz.habarta.typescript-generator', name: 'typescript-generator-gradle-plugin', version: '2.34.976'
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'idea'
  id 'pmd'
  id 'jacoco'
  id 'org.owasp.dependencycheck' version '6.5.0.1'
  id 'org.sonarqube' version '3.3'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.6.1'
  id 'com.github.ben-manes.versions' version '0.38.0'
  id 'hmcts.ccd.sdk' version '0.24.8'
}

apply plugin: 'cz.habarta.typescript-generator'
apply plugin: 'com.github.ben-manes.versions'

group = 'uk.gov.hmcts'
version = '0.0.1'

ccd {
  configDir = file('ccd-definitions/definitions')
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
  }

  ccdTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/ccdTest/java')
    }
  }
}

idea {
  module {
    testSourceDirs += file('src/integrationTest/java')
    testSourceDirs += file('src/functionalTest/java')
    testSourceDirs += file('src/ccdTest/java')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntime.extendsFrom testRuntime
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntime
  ccdTestImplementation.extendsFrom testImplementation
  ccdTestRuntime.extendsFrom testRuntime
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
  useJUnitPlatform()
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task buildCCDXlsx(type: Exec, dependsOn: generateCCDConfig) {
  group 'ccd tasks'
  commandLine './bin/ccd-build-definition.sh'
}

task ccdTest(type: Test, dependsOn: buildCCDXlsx) {
  description = "Runs ccd tests"
  group = "Verification"
  testClassesDirs = sourceSets.ccdTest.output.classesDirs
  classpath = sourceSets.ccdTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
  setClasspath(sourceSets.functionalTest.runtimeClasspath)
  include "uk/gov/hmcts/divorce/smoke/**"
}

task highLevelDataSetup(type: JavaExec) {
  mainClass.set("uk.gov.hmcts.divorce.common.ccd.HighLevelDataSetupApp")
  classpath += sourceSets.main.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.18.0"
  sourceSets = [sourceSets.main]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("${rootDir}/config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.getRequired().set(true)
    csv.getRequired().set(false)
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  outputDirectory = 'build/reports/'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

def sonarExclusions = [
  '**/uk/gov/hmcts/divorce/common/ccd/**',
  '**/uk/gov/hmcts/divorce/CaseApiApplication.java',
  '**/uk/gov/hmcts/divorce/common/config/**'
]

sonarqube {
  properties {
    property "sonar.projectName", "Divorce :: nfdiv-case-api"
    property "sonar.projectKey", "uk.gov.hmcts.reform:nfdiv-case-api"
    property "sonar.coverage.jacoco.xmlReportPaths", "${projectDir}/build/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.exclusions", sonarExclusions.join(', ')
  }
}


// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.70'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '31.0.1-jre') {
      entry 'guava'
    }
  }
  imports {
    mavenBom 'com.fasterxml.jackson:jackson-bom:2.12.4'
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

def versions = [
  awaitility        : '1.7.0',
  jackson           : '2.12.4',
  junit             : '5.8.2',
  junitPlatform     : '1.8.2',
  lombok            : '1.18.20',
  reformsJavaLogging: '5.1.7',
  springBoot        : springBoot.class.package.implementationVersion,
  s2sClient         : '4.0.0'
]

ext["rest-assured.version"] = '4.4.0'

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

dependencies {
// uncomment for local version
//implementation group: 'com.github.hmcts', name: 'ccd-config-generator', version: 'DEV-SNAPSHOT'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springBoot

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.5.13'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.0'

  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '3.17.2-RELEASE'

  implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformsJavaLogging
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformsJavaLogging
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-spring', version: versions.reformsJavaLogging

  implementation group: 'uk.gov.hmcts.reform', name: 'idam-client', version: '2.0.0'
  implementation group: 'com.github.hmcts', name: 'ccd-client', version: '4.8.3'

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.14.1'

  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.45'

  implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  implementation group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: versions.s2sClient

  implementation group: 'com.github.hmcts', name: 'send-letter-client', version: '3.0.10'
  implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '7.15.1'

  implementation group: 'com.github.hmcts', name: 'befta-fw', version: '8.6.0-prerelease'

  implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.1'
  implementation group: 'org.springframework', name: 'spring-aspects', version: '5.3.12'

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
    exclude(module: 'commons-logging')
    exclude(module: 'android-json')
  }

  testImplementation group: 'io.rest-assured', name: 'rest-assured'

  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.21.0'
  testImplementation group: 'org.assertj', name: 'assertj-guava', version: '3.4.0'
  testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-contract-stub-runner', version: '3.1.0'

  testImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.1.0'

  testImplementation group: 'com.jayway.awaitility', name: 'awaitility', version: versions.awaitility

  testImplementation group: 'com.github.stefanbirkner', name: 'system-lambda', version: '1.2.0'
  testImplementation group: 'io.github.openfeign', name: 'feign-jackson', version: '11.7'

  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  ccdTestImplementation sourceSets.main.runtimeClasspath
  ccdTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '7.0.0'
  functionalTestImplementation group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: versions.s2sClient

  ccdTestImplementation 'com.github.hmcts:rse-cft-lib:0.1.0'
  ccdTestImplementation 'org.springframework.security:spring-security-test'
}

mainClassName = 'uk.gov.hmcts.divorce.CaseApiApplication'

ext.getVaultSecret = { String secretName ->
  new ByteArrayOutputStream().withStream { os ->
    exec {
      commandLine 'az', 'keyvault', 'secret', 'show', '--vault-name', 'nfdiv-aat', '-o', 'tsv', '--query', 'value', '--name', secretName
      standardOutput = os
    }
    return os.toString().replace('\n', '')
  }
}

bootRun {
  doFirst() {
    if (System.getenv("IDAM_API_BASEURL") == null) {
      println("Loading secrets from vault")

      systemProperty 'IDAM_API_BASEURL', 'https://idam-api.aat.platform.hmcts.net'

      systemProperty 'UK_GOV_NOTIFY_API_KEY', getVaultSecret('uk-gov-notify-api-key')
      systemProperty 'S2S_SECRET', getVaultSecret('s2s-case-api-secret')
      systemProperty 'IDAM_CASEWORKER_USERNAME', getVaultSecret('idam-caseworker-username')
      systemProperty 'IDAM_CASEWORKER_PASSWORD', getVaultSecret('idam-caseworker-password')
      systemProperty 'IDAM_SYSTEM_UPDATE_USERNAME', getVaultSecret('idam-systemupdate-username')
      systemProperty 'IDAM_SYSTEM_UPDATE_PASSWORD', getVaultSecret('idam-systemupdate-password')
      systemProperty 'IDAM_SOLICITOR_USERNAME', getVaultSecret('idam-solicitor-username')
      systemProperty 'IDAM_SOLICITOR_PASSWORD', getVaultSecret('idam-solicitor-password')
      systemProperty 'IDAM_CLIENT_SECRET', getVaultSecret('idam-secret')

      if (System.getenv("BOOT_RUN_DOCKER")) {
        systemProperty 'IDAM_API_BASEURL', 'http://localhost:5000'
        systemProperty 'CASE_DATA_STORE_BASEURL', 'http://localhost:4452'
      }
    }
  }
}

bootJar {
  getArchiveFileName().set(provider {
    'nfdiv-case-api.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

functional {
  doFirst() {
    if (System.getenv("OAUTH2_CLIENT_SECRET") == null) {
      println("Loading secrets from vault")

      systemProperty 'IDAM_API_BASEURL', 'https://idam-api.aat.platform.hmcts.net'

      systemProperty 'UK_GOV_NOTIFY_API_KEY', getVaultSecret('uk-gov-notify-api-key')
      systemProperty 'S2S_SECRET', getVaultSecret('s2s-case-api-secret')
      systemProperty 'IDAM_CASEWORKER_USERNAME', getVaultSecret('idam-caseworker-username')
      systemProperty 'IDAM_CASEWORKER_PASSWORD', getVaultSecret('idam-caseworker-password')
      systemProperty 'IDAM_SYSTEM_UPDATE_USERNAME', getVaultSecret('idam-systemupdate-username')
      systemProperty 'IDAM_SYSTEM_UPDATE_PASSWORD', getVaultSecret('idam-systemupdate-password')
      systemProperty 'IDAM_SOLICITOR_USERNAME', getVaultSecret('idam-solicitor-username')
      systemProperty 'IDAM_SOLICITOR_PASSWORD', getVaultSecret('idam-solicitor-password')
      systemProperty 'OAUTH2_CLIENT_SECRET', getVaultSecret('idam-secret')
    }
  }
}

generateTypeScript {
  jsonLibrary = 'jackson2'
  classPatterns = [
    'uk.gov.hmcts.divorce.**.model.*',
    'uk.gov.hmcts.ccd.sdk.type.*',
  ]
  outputFile = 'build/ts/index.ts'
  outputKind = 'module'
  outputFileType = 'implementationFile'
  mapEnum = 'asEnum'
  mapDate = 'asString'
  group = 'ccd tasks'

  doLast {
    exec {
      commandLine 'yarn', 'prepublishOnly'
    }
  }
}

ccd {
  configDir = file('build/definitions')
}

tasks.register('stopCaseApiContainer') {
  group = 'ccd tasks'

  doFirst {
    exec {
      executable "sh"
      args "-c", "docker stop nfdiv-case-api || exit 0"
    }
  }
}

def reloadCcd = tasks.register('reloadCcd') {
  group = 'ccd tasks'
  doLast {
    exec {
      file('../.env').readLines().each() {
        def (key, value) = it.tokenize('=')
        environment key, value
      }
      commandLine './bin/process-and-import-ccd-definition.sh'
    }
    exec {
      file('../.env').readLines().each() {
        def (key, value) = it.tokenize('=')
        environment key, value
      }
      commandLine 'docker-compose', '-f', '../docker-compose.yml', 'up', '--build', '-d', 'nfdiv-case-api'
    }
  }
}

reloadCcd.configure {
  dependsOn assemble, generateCCDConfig
}

generateCCDConfig.dependsOn stopCaseApiContainer

