buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath group: 'cz.habarta.typescript-generator', name: 'typescript-generator-gradle-plugin', version: '3.2.1263'
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'idea'
  id 'pmd'
  id 'jacoco'
  id 'org.owasp.dependencycheck' version '12.1.1'
  id 'org.sonarqube' version '6.1.0.5360'
  id 'org.springframework.boot' version '3.4.5'
  id 'com.github.ben-manes.versions' version '0.52.0'
  id 'hmcts.ccd.sdk' version '5.5.21'
  id 'com.github.hmcts.rse-cft-lib' version '0.19.1705'
}

apply plugin: 'cz.habarta.typescript-generator'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.spring.dependency-management'

group = 'uk.gov.hmcts'
version = '0.0.1'

ccd {
  configDir = file('ccd-definitions/definitions')
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

idea {
  module {
    testSourceDirs += file('src/integrationTest/java')
    testSourceDirs += file('src/functionalTest/java')
  }
}

ext['elasticsearch.version'] = '7.16.3'

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntime.extendsFrom testRuntime
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntime
  cftlibTestImplementation.extendsFrom testImplementation
  cftlibTestRuntime.extendsFrom testRuntime
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
  useJUnitPlatform()
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
  jvmArgs += "-Xmx3g"
  environment 'ADMIN_UNLINK_APPLICANT_2_ENABLED', 'true'
}

// Invoked from CftLibConfig when booting with CCD.
task buildCCDXlsx(type: Exec) {
  group 'ccd tasks'
  commandLine './bin/ccd-build-definition.sh'
  inputs.dir layout.buildDirectory.dir('definitions')
  outputs.dir layout.buildDirectory.dir('ccd-config')
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
  setClasspath(sourceSets.functionalTest.runtimeClasspath)
  include "uk/gov/hmcts/divorce/smoke/**"
}

task highLevelDataSetup(type: JavaExec) {
  mainClass.set("uk.gov.hmcts.divorce.common.ccd.HighLevelDataSetupApp")
  classpath += sourceSets.main.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.14.0'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.18.0"
  sourceSets = [sourceSets.main]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("${rootDir}/config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.getRequired().set(true)
    csv.getRequired().set(false)
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  outputDirectory = 'build/reports/'
  scanConfigurations = ['runtimeClasspath']

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

def sonarExclusions = [
  '**/uk/gov/hmcts/divorce/common/ccd/**',
  '**/uk/gov/hmcts/divorce/common/event/CreateTestCase.java',
  '**/uk/gov/hmcts/divorce/CaseApiApplication.java',
  '**/uk/gov/hmcts/divorce/notification/GetAllTemplatesFromNotify.java',
  '**/uk/gov/hmcts/divorce/common/config/**',
  '**/uk/gov/hmcts/divorce/divorcecase/model/**',
  '**/uk/gov/hmcts/divorce/divorcecase/tab/**',
  '**/uk/gov/hmcts/divorce/systemupdate/event/SystemCorrectTtl.java'
]

sonarqube {
  properties {
    property "sonar.projectName", "Divorce :: nfdiv-case-api"
    property "sonar.projectKey", "uk.gov.hmcts.reform:nfdiv-case-api"
    property "sonar.coverage.jacoco.xmlReportPaths", "${projectDir}/build/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.exclusions", sonarExclusions.join(', ')
  }
}


// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.70'
    //CVE-2020-23064
    dependency group: 'org.json', name: 'json', version: '20250107'
  }
}

repositories {
  mavenLocal()
  mavenCentral()
}

def versions = [
  jackson      : '2.18.3',
  junit        : '5.11.4',
  junitPlatform: '1.11.4',
  lombok       : '1.18.38',
  springBoot   : springBoot.class.package.implementationVersion
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

dependencies {
// uncomment for local version
//  implementation group: 'com.github.hmcts', name: 'ccd-config-generator', version: 'DEV-SNAPSHOT'
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.1'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.24.3'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.24.3'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.24.3'
  implementation group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '7.8.0'
  implementation(group: 'com.github.hmcts', name: 'befta-fw', version: '9.2.4') {
    exclude group: 'org.apache.commons', module: 'commons-compress'
    exclude group: 'com.google.guava', module: 'guava'
    exclude group: 'org.apache.poi', module: 'poi-ooxml'
    exclude group: 'org.apache.poi', module: 'poi'
    exclude group: 'com.launchdarkly', module: 'launchdarkly-java-server-sdk'
  }
  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.1.0'
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.4'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.9'
  implementation group: 'com.github.hmcts', name: 'send-letter-client', version: '5.0.3'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.2'
  implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.59.2'

  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.6'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.retry', name: 'spring-retry'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '4.1.5'

  implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '7.17.28'
  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.2.1-RELEASE'
  implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0'
  implementation group: 'commons-validator', name: 'commons-validator', version: '1.9.0'
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.2.0'

  implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  implementation group: 'org.apache.commons', name: 'commons-jcs3-core', version: '3.2.1'
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  testImplementation libraries.junit5
  testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
    exclude(module: 'commons-logging')
    exclude(module: 'android-json')
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testImplementation group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
  testImplementation group: 'com.github.stefanbirkner', name: 'system-lambda', version: '1.2.1'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.3'
  testImplementation group: 'org.assertj', name: 'assertj-guava', version: '3.27.3'
  testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-contract-stub-runner', version: '4.1.5'
  testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.1.4'
  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0'
  testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
  testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
  testImplementation group: 'io.rest-assured', name: 'rest-assured'
  testImplementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '3.0.5'
  testImplementation group: 'org.simplify4u', name: 'slf4j2-mock', version: '2.3.0'
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.3', classifier: 'all'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.2.0'

  // Provides fast-reload of just the NFDIV service.
  cftlibImplementation 'org.springframework.boot:spring-boot-devtools'
  cftlibTestImplementation 'com.microsoft.playwright:playwright:1.52.0'
  cftlibTestImplementation 'org.junit-pioneer:junit-pioneer:2.3.0'
}

mainClassName = 'uk.gov.hmcts.divorce.CaseApiApplication'

task loadEnvSecrets() {
  doLast {
    if (!project.file('./.aat-env').exists()) {
      new ByteArrayOutputStream().withStream { os ->
        exec {
          commandLine 'az', 'keyvault', 'secret', 'show', '--vault-name', 'nfdiv-aat', '-o', 'tsv', '--query', 'value', '--name', 'nfdiv-case-api-dot-env'
          standardOutput = os
        }
        project.file('./.aat-env').write(new String(os.toString().replace('\n', '').decodeBase64(), java.nio.charset.StandardCharsets.UTF_8))
      }
    }
  }
}

task reloadEnvSecrets(group: 'ccd tasks', dependsOn: [loadEnvSecrets]) {
  doFirst {
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').delete()
    }
  }
}

bootRun {
  doFirst() {
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').readLines().each() {
        def index = it.indexOf("=")
        def key = it.substring(0, index)
        def value = it.substring(index + 1)
        environment key, value
      }
    }
  }
}

bootJar {
  getArchiveFileName().set(provider {
    'nfdiv-case-api.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

functional {
  doFirst() {
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').readLines().each() {
        def index = it.indexOf("=")
        def key = it.substring(0, index)
        def value = it.substring(index + 1)
        environment key, value
      }
    }
  }
}

generateTypeScript {
  jsonLibrary = 'jackson2'
  classPatterns = [
    'uk.gov.hmcts.divorce.**.model.*',
    'uk.gov.hmcts.ccd.sdk.type.*',
  ]
  outputFile = 'build/ts/index.ts'
  outputKind = 'module'
  outputFileType = 'implementationFile'
  mapEnum = 'asEnum'
  mapDate = 'asString'
  group = 'ccd tasks'

  doLast {
    exec {
      commandLine 'yarn', 'prepublishOnly'
    }
  }
}

ccd {
  configDir = file('build/definitions')
}

bootWithCCD {
  group = 'ccd tasks'
  dependsOn(loadEnvSecrets)

  doFirst() {
    project.file('./.aat-env').readLines().each() {
      def index = it.indexOf("=")
      def key = it.substring(0, index)
      def value = it.substring(index + 1)
      environment key, value
    }
    environment 'CITIZEN_UPDATE_CASE_STATE_ENABLED', 'true'
    environment 'LOGGING_LEVEL_UK_GOV_HMCTS_DIVORCE', 'info'
    environment 'ROLE_ASSIGNMENT_URL', 'http://localhost:4096'
    environment 'CASE_DATA_STORE_BASEURL', 'http://localhost:4452'
    environment 'XUI_MO_PORT', '3009'
    environment 'CREATE_PAPER_CASE_MOCK_NOTIFICATION', 'true'
  }
}

cftlibTest {
  // IDAM simulator will be started on port 5000,
  // S2S simulator on port 8489
  authMode = uk.gov.hmcts.rse.AuthMode.Local
  group = 'verification'

  systemProperties([
    // Configuration parameters to execute top-level classes in parallel but methods in same thread
    'junit.jupiter.execution.parallel.enabled'             : 'false',
    'junit.jupiter.execution.parallel.mode.default'        : 'same_thread',
    'junit.jupiter.execution.parallel.mode.classes.default': 'concurrent',
  ])

  environment 'PLAYWRIGHT_SKIP_BROWSER_DOWNLOAD', '1'
  environment 'RD_PROFESSIONAL_API_URL', 'http://host.docker.internal:8765'
  environment 'PRD_API_BASEURL', 'http://localhost:8765'
  environment 'DOC_ASSEMBLY_URL', 'http://localhost:8765'
  environment 'FEE_API_URL', 'http://localhost:8765'
  environment 'PAYMENT_API_BASEURL', 'http://localhost:8765'
  environment 'CCD_DOCUMENT_URL_PATTERN', '.*'
  environment 'DM_STORE_BASE_URL', 'http://localhost:8765'
  environment 'CASE_DOCUMENT_AM_API_ATTACH_DOCUMENT_ENABLED', 'false'
  environment 'CASE_DATA_STORE_BASEURL', 'http://localhost:4452'
  environment 'ROLE_ASSIGNMENT_URL', 'http://localhost:4096'
  environment 'CCD_S2S_AUTHORISED_SERVICES_CASE_USER_ROLES', 'nfdiv_case_api'
  environment 'DATA_STORE_S2S_AUTHORISED_SERVICES', 'ccd_gw,fpl_case_service,ccd_data,ccd_ps,divorce_frontend,payment-api,xui_webapp,nfdiv_case_api,ccd_case_document_am_api,am_role_assignment_service'
}

tasks.withType(Test).configureEach {
  maxParallelForks = Math.min(Runtime.runtime.availableProcessors().intdiv(2), 4) ?: 1
  jvmArgs(["--add-opens=java.base/java.lang=ALL-UNNAMED",
           "--add-opens=java.base/java.util=ALL-UNNAMED"])
}

// exclude the logback binding is coming in via cftlib
configurations.cftlibTestImplementation {
  exclude group: 'ch.qos.logback', module: 'logback-classic'
}

task installChromium(type: JavaExec) {
  // group = "Execution"
  // description = "Run the main class with JavaExecTask"
  classpath = sourceSets.cftlibTest.runtimeClasspath
  main = 'com.microsoft.playwright.CLI'
  args = ['install', 'chromium']
}

configurations.all {
  exclude group: 'com.vaadin.external.google', module: 'android-json'
}

tasks.cftlibTest.dependsOn(installChromium)

generateCCDConfig.doLast {
  copy {
    from file('ccd-definitions')
    into layout.buildDirectory.dir('definitions/NFD')
  }
}

task fortifyScan(type: JavaExec) {
    mainClass = 'uk.gov.hmcts.fortifyclient.FortifyClientMainApp'
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

